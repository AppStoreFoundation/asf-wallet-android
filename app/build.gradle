import groovy.json.JsonSlurper

buildscript {
  repositories {
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath 'io.fabric.tools:gradle:1.25.2'
  }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'

repositories {
  maven { url 'https://maven.fabric.io/public' }
}


android {
  compileSdkVersion 27
  buildToolsVersion '27.0.3'
  defaultConfig {

    def inputFile = new File("appcoins-services.json")
    def json = new JsonSlurper().parseText(inputFile.text)
    buildConfigField 'String', 'DEFAULT_OEM_ADREESS',
        "\"" + json.oems.default_address + "\""
    buildConfigField 'String', 'DEFAULT_STORE_ADREESS',
        "\"" + json.stores.default_address + "\""

    applicationId "com.asfoundation.wallet"
    minSdkVersion 18
    targetSdkVersion 27
    versionCode 4
    versionName "0.2.2a"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    vectorDrawables.useSupportLibrary = true
    multiDexEnabled = true

    buildConfigField 'int', 'DB_VERSION', '5'

    buildConfigField 'String', 'ROPSTEN_DEFAULT_TOKEN_SYMBOL',
        "\"" + project.ROPSTEN_DEFAULT_TOKEN_SYMBOL + "\""
    buildConfigField 'String', 'ROPSTEN_DEFAULT_TOKEN_ADDRESS',
        "\"" + project.ROPSTEN_DEFAULT_TOKEN_ADDRESS + "\""
    buildConfigField 'String', 'MAIN_NETWORK_DEFAULT_TOKEN_NAME',
        "\"" + project.MAIN_NETWORK_DEFAULT_TOKEN_NAME + "\""
    buildConfigField 'int', 'ROPSTEN_DEFAULT_TOKEN_DECIMALS', project.ROPSTEN_DEFAULT_TOKEN_DECIMALS
    manifestPlaceholders.fabricApiKey = "${project.ASF_WALLET_FABRIC_KEY}"
    buildConfigField 'String', 'ASF_IAB_CONTRACT_ADDRESS',
        "\"" + project.ASF_IAB_CONTRACT_ADDRESS + "\""

    buildConfigField 'String', 'MAIN_NETWORK_DEFAULT_TOKEN_SYMBOL',
        "\"" + project.MAIN_NETWORK_DEFAULT_TOKEN_SYMBOL + "\""
    buildConfigField 'String', 'MAIN_NETWORK_DEFAULT_TOKEN_ADDRESS',
        "\"" + project.MAIN_NETWORK_DEFAULT_TOKEN_ADDRESS + "\""
    buildConfigField 'String', 'ROPSTEN_DEFAULT_TOKEN_NAME',
        "\"" + project.ROPSTEN_DEFAULT_TOKEN_NAME + "\""
    buildConfigField 'int', 'MAIN_NETWORK_DEFAULT_TOKEN_DECIMALS',
        project.MAIN_NETWORK_DEFAULT_TOKEN_DECIMALS
  }

  signingConfigs {
    release {
      storeFile = file(project.ASF_WALLET_STORE_FILE)
      storePassword = project.ASF_WALLET_STORE_PASSWORD
      keyAlias = project.ASF_WALLET_KEY_ALIAS
      keyPassword = project.ASF_WALLET_KEY_PASSWORD
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
    debug {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      applicationIdSuffix ".dev"
      applicationVariants.all { variant -> renameArtifact(defaultConfig)
      }
    }
  }
  packagingOptions {
    exclude 'META-INF/NOTICE' // will not include NOTICE file
    exclude 'META-INF/LICENSE' // will not include LICENSE file
    // as noted by @Vishnuvathsan you may also need to include
    // variations on the file name. It depends on your dependencies.
    // Some other common variations on notice and license file names
    //exclude 'META-INF/notice'
    //exclude 'META-INF/notice.txt'
    //exclude 'META-INF/license'
    //exclude 'META-INF/license.txt'
  }
  lintOptions {
    abortOnError false
  }
  compileOptions {
    targetCompatibility 1.8
    sourceCompatibility 1.8
  }


  tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease')
      task.dependsOn('connectedAndroidTest')
  }
}

project.ext {
  retrofitVersion = "2.3.0"
  okhttpVersion = "3.9.0"
  picassoVersion = "2.5.2"
  supportVersion = "27.1.0"
  web3jVersion = "3.0.1-android"
  gethVersion = "1.8.0"
  gsonVersion = "2.8.2"
  rxJavaVersion = "2.1.6"
  rxAndroidVersion = "2.0.1"
  daggerVersion = "2.13"
  erc681Version = "0.36"
}

dependencies {
  // Etherium client
  implementation "org.web3j:core:$project.web3jVersion"
  implementation "org.ethereum:geth:$project.gethVersion"
  // Http client
  implementation "com.squareup.retrofit2:retrofit:$project.retrofitVersion"
  implementation "com.squareup.retrofit2:converter-gson:$project.retrofitVersion"
  implementation "com.squareup.retrofit2:adapter-rxjava2:$project.retrofitVersion"
  implementation "com.squareup.okhttp3:okhttp:$project.okhttpVersion"
  implementation "com.google.code.gson:gson:$project.gsonVersion"
  implementation "com.squareup.picasso:picasso:$project.picassoVersion"

  implementation "android.arch.lifecycle:runtime:1.1.1"
  implementation "android.arch.lifecycle:extensions:1.1.1"

  implementation "com.android.support:appcompat-v7:$project.supportVersion"
  implementation "com.android.support:design:$project.supportVersion"
  implementation "com.android.support:support-vector-drawable:$project.supportVersion"
  implementation "com.android.support:recyclerview-v7:$project.supportVersion"
  implementation "com.android.support:multidex:1.0.3"
  // Bar code scanning
  implementation "com.google.zxing:core:3.3.1"
  implementation "com.google.android.gms:play-services-vision:12.0.0"
  // Sugar
  implementation "com.android.support.constraint:constraint-layout:1.0.2"
  implementation "com.github.apl-devs:appintro:v4.2.2"
  implementation 'com.romandanylyk:pageindicatorview:1.0.0'
  implementation "com.journeyapps:zxing-android-embedded:3.2.0@aar"
  // ReactiveX
  implementation "io.reactivex.rxjava2:rxjava:$project.rxJavaVersion"
  implementation "io.reactivex.rxjava2:rxandroid:$project.rxAndroidVersion"
  // Dagger 2
  // Dagger core
  implementation "com.google.dagger:dagger:$project.daggerVersion"
  annotationProcessor "com.google.dagger:dagger-compiler:$project.daggerVersion"
  // Dagger Android
  implementation "com.google.dagger:dagger-android-support:$project.daggerVersion"
  annotationProcessor "com.google.dagger:dagger-android-processor:$project.daggerVersion"
  // if you are not using support library, include this instead
  implementation "com.google.dagger:dagger-android:$project.daggerVersion"

  implementation "com.github.walleth.kethereum:erc681:$project.erc681Version"
  // Tests
  testImplementation "junit:junit:4.12"
  testImplementation "org.mockito:mockito-core:2.15.0"
  androidTestImplementation("com.android.support.test.espresso:espresso-core:2.2.2", {
    exclude group: "com.android.support", module: "support-annotations"
  })
  androidTestImplementation('tools.fastlane:screengrab:1.1.0', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  // Fabric
  implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
    transitive = true
  }
  implementation('com.crashlytics.sdk.android:answers:1.4.1@aar') {
    transitive = true
  }
  // PW
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation project(":tn")
  implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'

  // Pincode Lollipin
  //    compile('com.github.omadahealth:lollipin:2.1.0@aar') {
  //        transitive = true
  //    }
}

def renameArtifact(defaultConfig) {
  android.applicationVariants.all { variant ->
    variant.outputs.all {
      def formattedDate = new Date().format('yyMMdd-HHmm')
      def fileName = "ASF_Wallet_V${defaultConfig.versionCode}_${formattedDate}_${variant.name}"
      outputFileName = new File("${fileName}.apk")
    }
  }
}